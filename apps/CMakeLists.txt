function(add_app target)
  add_executable(${target} ${ARGN} $<$<BOOL:${KAMINPAR_USE_BACKWARD_CPP}>:${BACKWARD_ENABLE}>)
  target_include_directories(${target} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR})
  target_link_libraries(${target} PRIVATE kaminpar_impl)
  if (KAMINPAR_USE_BACKWARD_CPP)
    add_backward(${target})
  endif ()
  install(TARGETS ${target})
  message(STATUS "App ${target}")
endfunction()

function(add_distributed_app target)
  if (TARGET dkaminpar_impl)
    add_app(${target} ${ARGN})
    target_link_libraries(${target} PRIVATE dkaminpar_impl)
  else ()
    message(STATUS "Cannot add distributed app: distributed library is not available, presumably because MPI"
        " could not be found.")
  endif ()
endfunction()

add_app(KaMinPar kaminpar.cc)
add_distributed_app(dKaMinPar dkaminpar.cc)

add_subdirectory(tools)
add_subdirectory(benchmarks)
